plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'com.commercesystems'
version = '0.0.2'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}

dependencies {
    api 'com.fasterxml.jackson.core:jackson-annotations:2.17.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.17.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.17.2'
    api('org.openapitools:jackson-databind-nullable:0.2.6') {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    }
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
    implementation 'org.apache.httpcomponents:httpmime:4.5.14'
    implementation 'commons-io:commons-io:2.15.1'
    implementation 'org.bouncycastle:bcpg-jdk15on:1.70'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = 'Klaviyo Java SDK'
                description = 'SDK enabling Java developers to easily integrate with the Klaviyo API.'
                url = 'https://github.com/commercesystems/klaviyo-java'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'commercesystems'
                        name = 'Commerce Systems'
                        email = 'aaron@commercesystems.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/commercesystems/klaviyo-java.git'
                    developerConnection = 'scm:git:ssh://git@github.com/commercesystems/klaviyo-java.git'
                    url = 'https://github.com/commercesystems/klaviyo-java'
                }
            }
        }
    }
}

tasks.register('packageForUpload', Zip) {
    archiveBaseName.set('klaviyo-java-sdk')
    archiveVersion.set(version)
    destinationDirectory.set(file("${buildDir}/distribution"))

    from("${buildDir}/libs") {
        include('*.jar') // Include compiled JARs
    }
    from('README.md') // Include README
    from('LICENSE') // Include LICENSE
    from('build.gradle') // Include build.gradle (optional)
}